/**
 * Copyright 2020 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.4'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Improve build server performance by allowing disabling of pre-dexing
 * (see http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance.)
 */
project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if (rootProject.ext.has('preDexLibs')) {
            if ("com.android.build.gradle.AppPlugin" == plugin.class.name) {
                project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
            } else if ("com.android.build.gradle.LibraryPlugin" == plugin.class.name) {
                project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
            }
        }
    }
}

/**
 * This task will copy all of the source code in the `library` module into the `library-v3` module
 * and change its imports from the Maps SDK on Play Services to the Standalone V3 Beta Maps SDK.
 */
task copyGmsToV3BetaSourceCode(type: Copy) {
    group 'Custom'
    description 'Generates source code for Maps SDK for Android V3 BETA Utilities'
    from 'library/src/main/java'
    into 'library-v3/src/main/java'
    filter { line ->
        line.replace('com.google.android.gms.maps', 'com.google.android.libraries.maps')
    }
}

/**
 * This task will copy all of the tests in the `library` module into the `library-v3` module
 * and change its imports from the Maps SDK on Play Services to the Standalone V3 Beta Maps SDK.
 */
task copyGmsToV3BetaTests(type: Copy) {
    group 'Custom'
    description 'Generates tests for Maps SDK for Android V3 BETA Utilities'
    from 'library/src/test'
    into 'library-v3/src/test'
    filter { line ->
        line.replace('com.google.android.gms.maps', 'com.google.android.libraries.maps')
    }
}
